// apply Gradle plugins
plugins {
    id 'java'
    id 'maven-publish'
    id 'eclipse'
    id 'idea'

    id 'checkstyle'
    id 'org.cadixdev.licenser' version '0.6.1'
}

defaultTasks 'clean', 'licenseFormat', 'build'

// Project information
group = 'net.caseif.rosetta'
version = '1.1.4-SNAPSHOT'

// Extended project information
ext.projectName = 'rosetta'
ext.description = 'A localization API for Bukkit plugins.'
ext.inceptionYear = '2015'
ext.packaging = 'jar'
ext.author = 'Max Roncace'

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    deployerJars
}

// Project repositories
repositories {
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }

    mavenCentral()
}

// Project dependencies
dependencies {
    implementation 'org.bukkit:bukkit:1.8.8-R0.1-SNAPSHOT'

    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.9'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

processResources {
    from 'LICENSE'
}

// License header formatting
license {
    include '**/*.java'
    ignoreFailures false
}

// check code style
checkstyle {
    configProperties = [
            'name'        : project.name,
            'year'        : project.inceptionYear
    ]
    configFile = file('etc/checkstyle.xml')
}

tasks.withType(Checkstyle){
    exclude '**/*.properties'
    exclude '**/*.yml'
}

jar.manifest.mainAttributes(
        'Created-By': System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
        'Specification-Title': name,
        'Specification-Version': version,
        'Specitication-Vendor': author
)

task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Rosetta'
                description = project.ext.description
                url = 'http://github.com/caseif/Rosetta'

                scm {
                    url = 'https://github.com/caseif/Rosetta'
                    connection = 'scm:git:git://github.com/caseif/Rosetta.git'
                    developerConnection = 'scm:git:git@github.com:caseif/Rosetta.git'
                }

                licenses {
                    license {
                        name = 'BSD 3-Clause'
                        url = 'http://opensource.org/licenses/BSD-3-clause'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

wrapper {
    gradleVersion = '7.4'
}
